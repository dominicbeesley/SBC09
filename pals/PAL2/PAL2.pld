Name     PAL2        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g22v10      ;

/* *************** INPUT PINS *********************/

PIN 1    = E;
PIN 2    = !ENMMU;
PIN 3    = 16KMODE;
PIN 4    = QA19;
PIN 5    = QA20;
PIN 6    = QA21;
PIN 7    = !CSIO;
PIN 8    = !CSIOINT;
PIN 9    = A13;
PIN 10   = A14;
PIN 11   = A15;
// PIN 13

PIN 14   = !ENMMU1;
PIN 15   = !ENMMU0;
// PIN 16
PIN 17   = !CSRAM;
PIN 18   = !CSROM1;
PIN 19   = !CSROM0;
PIN 20   = !CSEXT;
// PIN 21
PIN 22   = QA14;
PIN 23   = QA13;

// QA21 QA20 Device
//    0    0    ROM0
//    0    1    ROM1
//    1    0    RAM
//    1    1    External

// Outputs

CSROM0  = ((ENMMU & !QA21 & !QA20) # (!ENMMU & A15)) & !CSIO;
CSROM1  =  (ENMMU & !QA21 &  QA20) & !CSIO;
CSRAM   = ((ENMMU &  QA21 & !QA20) # (!ENMMU & !A15)) & !CSIO;
CSEXT   =  (ENMMU & (QA21 &  QA20 # CSIO) & !CSIOINT);
ENMMU0  = (ENMMU & !16KMODE & !A13) # (ENMMU & 16KMODE);
ENMMU1  =  ENMMU & !16KMODE &  A13 ;

QA13 = (16KMODE & A13)  # (!16KMODE & QA19);

// When MMU Disabled, pass through A14->QA14 else tristate

QA14    = A14;
QA14.OE = !ENMMU;
