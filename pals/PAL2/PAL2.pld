Name     PAL2        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g22v10      ;

/* *************** INPUT PINS *********************/

PIN 1    = !ENMMU;
PIN 2    = A6;
PIN 3    = A7;
PIN 4    = A8;
PIN 5    = A9;
PIN 6    = A10;
PIN 7    = A11;
PIN 8    = A12;
PIN 9    = A13;
PIN 10   = A14;
PIN 11   = A15;
PIN 13   = 16KMODE;

PIN 14   = !ENMMU1;
PIN 15   = !ENMMU0;
PIN 16   = SPARE;
PIN 17   = !CSIOINT;
PIN 18   = !CSEXT;
PIN 19   = !CSRAM;
PIN 20   = !CSROM1;
PIN 21   = !CSROM0;
PIN 22   = QA20;
PIN 23   = QA19;

// Intermediates

// Decode &FC00-&FEFF
IO       = A15 & A14 & A13 & A12 & A11 & A10 & (!A9 # !A8);

// Decde &FE00-&FE3F
IOINT    = A15 & A14 & A13 & A12 & A11 & A10 & A9 & !A8 & !A7 & !A6;


// QA20 QA19 Device
//    0    0    ROM0
//    0    1    ROM1
//    1    0    RAM
//    1    1    External

// Outputs

CSROM0  = ((ENMMU & !QA20 & !QA19) # (!ENMMU &  A15 & !A14)) & !IO;
CSROM1  = ((ENMMU & !QA20 &  QA19) # (!ENMMU &  A15 &  A14)) & !IO;
CSRAM   = ((ENMMU &  QA20 & !QA19) # (!ENMMU & !A15       )) & !IO;
CSEXT   =  (ENMMU & (QA20 &  QA19 # IO) & !IOINT);
CSIOINT = IOINT;
ENMMU0  = (ENMMU & !16KMODE & !A13) # (ENMMU & 16KMODE);
ENMMU1  =  ENMMU & !16KMODE &  A13 ;
