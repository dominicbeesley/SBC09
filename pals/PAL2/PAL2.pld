Name     PAL2        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g22v10      ;

/* *************** INPUT PINS *********************/

PIN 1    = CLKX4;
PIN 2    = RnW;
PIN 3    = A14TO11;
PIN 4    = A6;
PIN 5    = A7;
PIN 6    = A8;
PIN 7    = A9;
PIN 8    = A10;
PIN 9    = A15;
PIN 10   = !ENMMU;
PIN 11   = MRDY;
PIN 13   = QA20;

PIN 14   = QA21;
PIN 15   = E;
PIN 16   = nRW;
PIN 17   = !CSROM;
PIN 18   = !CSRAM;
PIN 19   = !CSEXT;
PIN 20   = !CSEXTIO;
PIN 21   = !CSINTIO;
PIN 22   = EX;
PIN 23   = QX;

// In E-mode, drive E and Q to the CPU
//
// Note: E lags Q

FIELD count = [EX, QX];

$DEFINE s0 'b'00
$DEFINE s1 'b'01
$DEFINE s2 'b'11
$DEFINE s3 'b'10

SEQUENCE count {
  PRESENT s0
    NEXT s1;
  PRESENT s1
    NEXT s2;
  PRESENT s2
    NEXT s3;
  PRESENT s3
    IF MRDY NEXT s0;
    DEFAULT NEXT s3;
}


EX.AR = 'b'0;
EX.SP = 'b'0;
QX.AR = 'b'0;
QX.SP = 'b'0;

// Decode &FC00-&FEFF
IO       = A15 & A14TO11 & A10 & (!A9 # !A8);

// Decde &FE00-&FE3F
INTIO    = A15 & A14TO11 & A10 & A9 & !A8 & !A7 & !A6;


// QA21 QA20 Device
//    0    0    ROM
//    0    1    External
//    1    0    RAM
//    1    1    RAM (write protected)

// Outputs

CSROM   = ((ENMMU &  !QA21 & !QA20)         # (!ENMMU &  A15)) & !IO & E;
CSRAM   = ((ENMMU &   QA21 & (RnW # !QA20)) # (!ENMMU & !A15)) & !IO & E;
CSEXT   =  (ENMMU & (!QA21 &  QA20 # IO) & !INTIO)                   & E;
CSINTIO = INTIO                                                      & E;
CSEXTIO = IO & !INTIO                                                & E;
