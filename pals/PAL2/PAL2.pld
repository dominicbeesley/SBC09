Name     PAL2        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g22v10      ;

/* *************** INPUT PINS *********************/

PIN 1    = nENMMU;
PIN 2    = A6;
PIN 3    = A7;
PIN 4    = A8;
PIN 5    = A9;
PIN 6    = A10;
PIN 7    = A11;
PIN 8    = A12;
PIN 9    = A13;
PIN 10   = A14;
PIN 11   = A15;
PIN 13   = 16KMODE;

PIN 14   = nENMMU1;
PIN 15   = nENMMU0;
PIN 16   = SPARE;
PIN 17   = nCSIOINT;
PIN 18   = nCSEXT;
PIN 19   = nCSRAM;
PIN 20   = nCSROM1;
PIN 21   = nCSROM0;
PIN 22   = QA20;
PIN 23   = QA19;

// Intermediates

// Decode &FC00-&FEFF
IO       = A15 & A14 & A13 & A12 & A11 & A10 & (!A9 # !A8);

// Decde &FC00-&FC3F
IOINT    = A15 & A14 & A13 & A12 & A11 & A10 & !A9 & !A8 & !A7 & !A6;

// Outputs

nCSROM0  = !( !QA20 & !QA19 & !IO);
nCSROM1  = !( !QA20 &  QA19 & !IO);
nCSRAM   = !(  QA20 & !QA19 & !IO);
nCSEXT   = !( (QA20 &  QA19 #  IO) & !IOINT);
nCSIOINT = !(IOINT);
nENMMU0  = !((!nENMMU & !16KMODE & !A13) # (!nENMMU & 16KMODE));
nENMMU1  = !( !nENMMU & !16KMODE &  A13 );
